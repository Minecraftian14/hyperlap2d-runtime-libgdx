apply plugin: 'java-library'

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'games.rednblack.hyperlap2d'
version = '0.0.8'
def projectName = "Hyperlap2D Runtime"

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

configurations {
    provided
    implementation.extendsFrom(provided)
}

dependencies {
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    implementation "net.onedaybeard.artemis:artemis-odb:$artemisVersion"

    implementation "com.rafaskoberg.gdx:typing-label:$typingLabelVersion"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

task libraryJar(type: Jar, dependsOn: classes) {

    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    from configurations.provided.asFileTree.files.collect { zipTree(it) }

    manifest {
        attributes 'Implementation-Title': projectName,
                'Implementation-Version': project.version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {

    archiveClassifier.set("sources")
    from sourceSets.main.allSource

    manifest {
        attributes 'Implementation-Title': projectName,
                'Implementation-Version': project.version
    }

}

// Declaring artifacts for jitpack to recognise
artifacts {
    archives sourcesJar
    archives libraryJar
}
